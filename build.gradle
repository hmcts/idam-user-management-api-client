plugins {
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.owasp.dependencycheck' version '12.1.3'
	id 'checkstyle'
	id 'pmd'
	id 'jacoco'
	id 'com.github.ben-manes.versions' version '0.52.0'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

checkstyle {
	getConfigDirectory().set(new File(rootDir, "config/checkstyle"))
}

pmd {
	consoleOutput = true
	ruleSetFiles = files("$rootProject.projectDir/config/pmd/ruleset.xml")
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	springCloudVersion = "2025.0.0"
}

// before committing a change, make sure task still works
dependencyUpdates {
	def isNonStable = { String version ->
		def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
		def regex = /^[0-9,.v-]+$/
		return !stableKeyword && !(version ==~ regex)
	}
	rejectVersionIf { selection -> // <---- notice how the closure argument is named
		return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
	}
}

dependencyCheck {
	// Specifies if the build should be failed if a CVSS score above a specified level is identified.
	// range of 0-10 fails the build, anything greater and it doesn't fail the build
	failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
	suppressionFile = "$rootDir/config/owasp/suppressions.xml"
}

dependencies {
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.19.0'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.19.0'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	publications {
		Main(MavenPublication) {
			from components.java
			groupId project.group
			artifactId 'idam-user-management-api-client'
			version project.version
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}
	repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

bootJar {
	enabled = false
}
jar {
	enabled = true
	archiveClassifier = ''
}
